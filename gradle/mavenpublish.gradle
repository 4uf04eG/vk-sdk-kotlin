apply plugin: "com.vanniktech.maven.publish"

mavenPublish {
    useLegacyMode = !project.plugins.hasPlugin('org.jetbrains.kotlin.multiplatform')
    releaseSigningEnabled = isReleaseBuild()

    targets {
        installArchives {
            def url = file("${rootProject.buildDir}/localMaven").toURI().toString()
            project.logger.warn("publish: test: repo: $url")
            releaseRepositoryUrl = url
            snapshotRepositoryUrl = url
        }
    }
}

signing {
    def key = getGpgKey()
    def pwd = getGpgPassword()
    if (!key.isEmpty()) {
        useInMemoryPgpKeys(key, pwd)
    }
}

@SuppressWarnings("GrMethodMayBeStatic")
Boolean isReleaseBuild() {
    return !VERSION_NAME.contains("SNAPSHOT")
}

def getGpgKey() {
    return hasProperty('signingKey') ? signingKey : ""
}

def getGpgPassword() {
    return hasProperty('signingPassword') ? signingPassword : ""
}

apply from: "$rootDir/gradle/dokka.gradle"
